Phase: SCRules
Input:  Lookup Split Token NounChunk
Options: control = appelt


Macro: LIST

(
({NounChunk}{Token.string == ","})[0,10]
{NounChunk}
({Token.string == ","})?
{Token.category == CC}
{NounChunk}
)

// 3
Rule:SC2
//[(NP<subclass>,)* and] NP<subclass> (classify as) | (group in|into|as) | (fall into) | (belong to) [CN] NP<superclass>
//Thyroid medicines belong to the general group of hormone medicines.
//Thyroid medicines are classified as hormone medicines.

(
((LIST)|{NounChunk}):subclass
({Lookup.minorType == be})?
{Lookup.minorType == SCverbs}
({Token.category == DT})?
({Token.category == JJ})?
({Lookup.majorType == CN})?
({NounChunk}):superclass
)

-->
:superclass.Superclass = {rule="SC2"},
{
// "subclass" matches LHS label
  List annList = new ArrayList((AnnotationSet)bindings.get("subclass"));

  //sort the list by offset
  Collections.sort(annList, new OffsetComparator());

  //iterate through the matched annotations
  for(int i = 0; i < annList.size(); i++)
   {
    Annotation anAnn = (Annotation)annList.get(i);

// check that the new annotation is a NounChunk 

if ((anAnn.getType().equals("NounChunk")) )

{ 
    FeatureMap features = Factory.newFeatureMap();

// change this for a different rule name"
    features.put("rule", "SC2");
  
// change "Subclass" for a different annotation name
 annotations.add(anAnn.getStartNode(), anAnn.getEndNode(), "Subclass",
 features);

}
}
}
