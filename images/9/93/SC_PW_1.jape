Phase: SCRules
Input:  Lookup Split Token NounChunk
Options: control = appelt

Macro: LIST

(
({NounChunk}{Token.string == ","})[0,10]
{NounChunk}
({Token.string == ","})?
{Token.category == CC}
{NounChunk}
)


Macro: ADJLIST

(
({Token.category == JJ}{Token.string == ","})[0,10]
{Token.category == JJ}
({Token.string == ","})?
{Token.category == CC}
{Token.category == JJ}
)

// 17
Rule:SC_PW
// NP<class> include | comprise | consist of [(NP<class >,)* and] NP<class>
// Common mass storage devices include disk drives and tape drives.
// Reproductive structures in female insects include ovaries, bursa copulatrix and uterus.


(
({NounChunk}):superclass_whole
({Token.category == IN})?
({NounChunk})?
{Lookup.minorType == include}
(LIST):subclass_Di_EC_part
)

-->
:superclass_whole.SuperclassWhole = {rule=SC_PW},
{
// "subclass" matches LHS label
  List annList = new ArrayList((AnnotationSet)bindings.get("subclass_Di_EC_part"));

  //sort the list by offset
  Collections.sort(annList, new OffsetComparator());

  //iterate through the matched annotations
  for(int i = 0; i < annList.size(); i++)
   {
    Annotation anAnn = (Annotation)annList.get(i);

// check that the new annotation is a NounChunk 
if ((anAnn.getType().equals("NounChunk")) )

{ 
    FeatureMap features = Factory.newFeatureMap();

// change this for a different rule name"
    features.put("rule", "SC_PW");
  
// change "Subclass" for a different annotation name
 annotations.add(anAnn.getStartNode(), anAnn.getEndNode(), "Subclass_Di_ECPart",
 features);

}
}
}
